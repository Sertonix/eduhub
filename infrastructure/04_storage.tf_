###############################################################################
# Creation of a file storage system for the EduHub application
#####

# Create a bucket in Google cloud storage service (GCS).
resource "google_storage_bucket" "storage_bucket" {
  name     = var.storage_bucket_name
  location = var.region
}

# Create a Google service account to manage the storage bucket.
resource "google_service_account" "storage-bucket-service-account" {
  account_id   = "storage-object-admin-staging"
  display_name = "Storage Staging Service Account"
}

# Generate an IAM policy document that will be referenced by and applied to the created 
# bucket in the Google cloud storage service
data "google_iam_policy" "storage_admin" {
  binding {
    role = "roles/storage.objectAdmin"
    members = [
      "serviceAccount:${google_service_account.storage-bucket-service-account.email}",
    ]
  }
}

# Set the IAM policy for the bucket according to the above generated one and
# replace any existing policy already attached.
resource "google_storage_bucket_iam_policy" "policy_staging" {
  bucket      = google_storage_bucket.storage_bucket.name
  policy_data = data.google_iam_policy.storage_admin.policy_data
}
